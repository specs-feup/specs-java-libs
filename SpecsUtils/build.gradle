plugins {
    id 'distribution'
    id 'java'
    id 'jacoco'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    // Testing dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.5.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.5.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0' // For static mocking
    testImplementation group: 'org.junit-pioneer', name: 'junit-pioneer', version: '2.3.0' // For test retries
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.10.0'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test-resources'
        }
    }
}

// Test coverage configuration
jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.78 // 80% should be the minimum coverage, but I didn't get there with auto-generated tests
            }
        }
    }
}

// Make sure jacoco report is generated after tests
test {
    useJUnitPlatform()

    maxParallelForks = Runtime.runtime.availableProcessors() / 2

    finalizedBy jacocoTestReport
}
