plugins {
    id 'distribution'
    id 'java'
    id 'jacoco'
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

// Repositories providers
repositories {
    mavenCentral()
}

dependencies {
    implementation ':SpecsUtils'
    implementation ':XStreamPlus'
    implementation ':GuiHelper'

    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'

    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0' // For static mocking
    testImplementation 'org.junit-pioneer:junit-pioneer:2.3.0' // For test retries
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
}

// Project sources
sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }

    test {
        java {
            srcDir 'test'
            srcDir 'legacy-tests'
        }
    }
}

// Test coverage configuration
jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }

    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.50 // 80% minimum coverage is the normal but I'm ignoring the GUI classes
            }
        }
    }
}

// Make sure jacoco report is generated after tests
test {
    useJUnitPlatform()

    maxParallelForks = Runtime.runtime.availableProcessors()

    finalizedBy jacocoTestReport
}
